buildscript {
    repositories mavenRepositories
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:2.7.3")
        classpath("gradle.plugin.com.gorylenko.gradle-git-properties:gradle-git-properties:${gradleGitPropertiesVersion}")
        classpath("org.eclipse.jgit:org.eclipse.jgit") {
            version {
                strictly '5.13.0.202109080827-r'
            }
        }
    }
}

normalization {
    runtimeClasspath {
        ignore("**/*git.properties*")
        ignore("**/*build-info.properties*")
    }
}

apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'com.gorylenko.gradle-git-properties'
apply plugin: 'com.google.cloud.tools.jib'
apply from: "../gradle/jib.gradle"

def javaAgentPort = 6310
def debugPort = 5130
def componentName = "cloud-gateway-service"

setJib(componentName, javaAgentPort, debugPort)
jib.container.mainClass = 'org.zowe.apiml.cloudgatewayservice.CloudGatewayServiceApplication'

springBoot {
    // This statement tells the Gradle Spring Boot plugin to generate a file
    // build/resources/main/META-INF/build-info.properties that is picked up by Spring Boot to display
    // via /info endpoint
    buildInfo {
        properties {
            // Generate extra build info:
            additional = [
                by             : System.properties['user.name'],
                operatingSystem: "${System.properties['os.name']} (${System.properties['os.version']})",
                number         : System.getenv('BUILD_NUMBER') ? System.getenv('BUILD_NUMBER') : "n/a",
                machine        : InetAddress.localHost.hostName
            ]
        }
    }
}

gitProperties {
    gitPropertiesDir = new File("${project.rootDir}/${name}/build/resources/main/META-INF")
}

dependencies {

    implementation project(':common-service-core')
    implementation ('org.springframework.cloud:spring-cloud-starter-gateway:3.1.4'){
        exclude group: "org.yaml", module: "snakeyaml"
        exclude group: "org.bouncycastle", module: "bcprov-jdk15on"
    }
    implementation libraries.bcprov
    implementation libraries.snakeyaml
    implementation ('org.springframework.cloud:spring-cloud-starter-netflix-eureka-client:3.1.3'){
        exclude group: "com.google.guava", module: "guava"
    }
    implementation libraries.jackson_databind
    implementation libraries.guava
    implementation 'org.springframework.boot:spring-boot-starter-actuator:2.7.3'

    compileOnly libraries.lombok
    annotationProcessor libraries.lombok
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor:2.7.3'



    testImplementation 'org.springframework.boot:spring-boot-starter-test:2.7.3'
}

bootJar {
    archiveFileName = "cloud-gateway-service.jar"
    manifest {
        attributes 'Main-Class': 'org.springframework.boot.loader.PropertiesLauncher'
    }
    archiveClassifier = "application"
}

jar {
    enabled true
    archiveClassifier = ""
}

bootRun {
    if (project.hasProperty('args')) {
        args project.args.split(',')
    }
    systemProperties = System.properties
}

publishing {
    publications {
        mavenJavaFat(MavenPublication) {
            groupId = 'org.zowe.apiml'
            artifactId = 'cloud-gateway-fat-jar'

            artifact bootJar
        }
    }
}

task wrapper(type: Wrapper){
    gradleVersion = '7.2'
}
