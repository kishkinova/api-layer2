plugins {
    id "com.moowork.node" version "1.3.1"
}

node {
    // Version of node to use.
    version = rootProject.nodejsVersion

    // Base URL for fetching node distributions (change if you have a mirror).
    distBaseUrl = 'https://nodejs.org/dist'

    // If true, it will download node using above parameters.
    // If false, it will try to use globally installed node.
    download = false

    // Set the work directory for unpacking node
    workDir = file("${project.projectDir}/tools/nodejs")

    // Set the work directory for NPM
    npmWorkDir = file("${project.projectDir}/tools/npm")

    // Set the work directory where node_modules should be located
    nodeModulesDir = file("${project.projectDir}/frontend")
}

// =================================================
//
//  Please manage all task dependencies in NPM and
//  not here. This should be just a wrapper around
//  package.json
//
// =================================================

task jsClean(type: Delete) {
    group = 'node'
    delete 'bin', 'build', 'frontend/test-results', 'frontend/coverage', 'frontend/build', 'tools/nodejs'
}

////todo remove
//task npmTest(type: NpmTask) {
//    group = 'node'
//    description = "Javascript unit tests"
//    args = ['run', 'test']
//}

////todo remove
//task npmE2ETests(type: NpmTask, dependsOn: ':integration-tests:startUpCheck') {
//    group = 'node'
//    description = "Javascript End to End Tests"
//
//    args = ['run', 'cy:e2e:zowe-ci']
//}


task npmBuild(type: NpmTask) {
    group = 'node'
    description = "Produces errors in intellij run window but tests pass, dont panic. You can verify running the npm test directly"
    inputs.dir('frontend/src').withPathSensitivity(PathSensitivity.RELATIVE)
    inputs.dir('frontend/public').withPathSensitivity(PathSensitivity.RELATIVE)
    inputs.files('frontend/.env*').withPathSensitivity(PathSensitivity.RELATIVE)
    inputs.file('frontend/.eslintrc').withPathSensitivity(PathSensitivity.RELATIVE)
    inputs.file('frontend/package.json').withPathSensitivity(PathSensitivity.RELATIVE)

    outputs.dir('frontend/build')
    outputs.dir('frontend/test-resultstest-report-unit.html')
    outputs.dir('frontend/coverage')
    outputs.cacheIf { true }

    args = ['run', 'build']

}

clean.dependsOn jsClean
build.dependsOn npmBuild

jar {
    enabled false
}
