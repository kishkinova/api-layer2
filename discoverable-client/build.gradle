buildscript {
    repositories mavenRepositories
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("gradle.plugin.com.gorylenko.gradle-git-properties:gradle-git-properties:${gradleGitPropertiesVersion}")
        classpath("org.eclipse.jgit:org.eclipse.jgit") {
            version {
                strictly '5.13.0.202109080827-r'
            }
        }
    }
}

normalization {
    runtimeClasspath {
        ignore("**/*git.properties*")
        ignore("**/*build-info.properties*")
    }
}

apply plugin: 'org.springframework.boot'
apply plugin: 'com.gorylenko.gradle-git-properties'
apply plugin: 'com.google.cloud.tools.jib'
apply from: "../gradle/jib.gradle"

def javaAgentPort = 6302
def debugPort = 5122
def componentName = "discoverable-client"

setJib(componentName, javaAgentPort, debugPort)

springBoot {
    // This statement tells the Gradle Spring Boot plugin to generate a file
    // build/resources/main/META-INF/build-info.properties that is picked up by Spring Boot to display
    // via /info endpoint
    buildInfo {
        properties {
            // Generate extra build info:
            additional = [
                by: System.properties['user.name'],
                operatingSystem: "${System.properties['os.name']} (${System.properties['os.version']})",
                number: System.getenv('BUILD_NUMBER') ? System.getenv('BUILD_NUMBER') : "n/a",
                machine: InetAddress.localHost.hostName
            ]
        }
    }
}

gitProperties {
    gitPropertiesDir = new File("${project.rootDir}/${name}/build/resources/main/META-INF")
}

dependencies {
    implementation project(':onboarding-enabler-java')
    api project(':onboarding-enabler-spring')
    implementation project(':zaas-client')
    implementation libraries.spring_doc
    implementation libraries.spring_boot_starter
    implementation libraries.spring_boot_starter_actuator
    implementation libraries.snakeyaml
    implementation (libraries.spring_boot_starter_webflux) {
        exclude group: "io.netty", module: "netty-codec-http"
        exclude group: "io.netty", module: "netty-codec-http2"
        exclude group: "io.netty", module: "netty-handler-proxy"
        exclude group: "io.netty", module: "netty-resolver-dns-native-macos"
        exclude group: "io.netty", module: "netty-transport-native-epoll"
    }
    implementation libraries.netty_codec_http
    implementation libraries.netty_codec_http2
    implementation libraries.netty_handler_proxy
    implementation libraries.netty_resolver_dns_native_macos
    implementation libraries.netty_transport_native_epoll

    implementation (libraries.spring_boot_starter_websocket){
        exclude group: "org.springframework.boot", module: "spring-boot-starter-web"
    }
    implementation (libraries.spring_boot_starter_validation){
        exclude group: "org.apache.tomcat.embed", module: "tomcat-embed-el"
    }
    implementation libraries.spring_boot_starter_security
    implementation libraries.spring_boot_starter_aop
    implementation libraries.zowe_attls
    implementation libraries.spring_cloud_commons
    implementation libraries.spring_cloud_starter_bootstrap
    implementation libraries.spring_cloud_starter_hystrix
    implementation (libraries.spring_cloud_hystrix_dashboard){
        exclude group: "org.springframework.boot", module: "spring-boot-starter-web"
    }
    implementation libraries.spring_security_web
    implementation libraries.spring_security_config
    implementation libraries.spring_security_core
    implementation libraries.spring_security_crypto
    implementation libraries.tomcat_embed_core
    implementation libraries.jackson_core
    implementation libraries.jackson_databind
    implementation libraries.bootstrap
    implementation libraries.jquery

    implementation libraries.gson
    compileOnly libraries.lombok
    annotationProcessor libraries.lombok

    testImplementation libraries.spring_boot_starter_test
    testImplementation libraries.json_smart
}


bootJar.archiveFileName = bootJar.archiveBaseName.get() + ".jar"
