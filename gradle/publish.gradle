ext.enablers = [
    'onboarding-enabler-spring',
    'onboarding-enabler-java'
    //'onboarding-enabler-micronaut'
]

ext.javaLibraries = [
    'apiml-utility',
    'apiml-common',
    'apiml-security-common',
    'apiml-tomcat-common',
    'certificate-analyser',
    'common-service-core',
    'security-service-client-spring',
    'zaas-client',
    'apiml-sample-extension',
    'apiml-sample-extension-package',
    'apiml-extension-loader'
]

ext.servicesToPublish = [
    'apiml-common-lib-package',
    'api-catalog-services',
    'api-catalog-package',
    'caching-service',
    'caching-service-package',
    'discovery-service',
    'discovery-package',
    'cloud-gateway-service',
    'cloud-gateway-package'
]

ext.sdksToPublish = ext.enablers + ext.javaLibraries
ext.projectsToPublish = ext.servicesToPublish + ext.sdksToPublish

configure(subprojects.findAll { it.name in projectsToPublish }) {
    apply plugin: 'maven-publish'
    apply plugin: 'java'

    publishing {
        repositories.maven {
            credentials {
                username project.hasProperty("zowe.deploy.username") ? project.getProperty("zowe.deploy.username") : ""
                password project.hasProperty("zowe.deploy.password") ? project.getProperty("zowe.deploy.password") : ""
            }
            if (rootProject.version.endsWith("-SNAPSHOT")) {
                setUrl(artifactoryPublishingMavenSnapshotRepo)
            } else {
                setUrl(artifactoryPublishingMavenRepo)
            }
        }

        publications {
            mavenJava(MavenPublication) {
                pom {
                    licenses {
                        license {
                            name = 'Eclipse Public License, v2.0'
                            url = 'https://www.eclipse.org/legal/epl-2.0/'
                        }
                    }
                }
            }
        }
    }
}

configure(subprojects.findAll { it.name in sdksToPublish }) {
    publishing {
        publications {
            mavenJava(MavenPublication) {
                groupId 'org.zowe.apiml.sdk'
                version rootProject.version
                artifactId "${project.name}"

                from components.java

                pom.withXml {
                    asNode().dependencies.'*'.findAll() {
                        it.scope.text() == 'runtime' && project.configurations.implementation.allDependencies.find { dep ->
                            dep.name == it.artifactId.text()
                        }
                    }.each { it.scope*.value = 'compile' }
                }
            }
        }
    }

    tasks.withType(Jar) {
        manifest {
            attributes "Specification-Title": project.name
            attributes "Specification-Version": rootProject.version
            attributes "Specification-Vendor": "Zowe org"

            attributes "Implementation-Title": project.name
            attributes "Implementation-Version": rootProject.version
            attributes "Implementation-Vendor": "Zowe org"
            attributes "Implementation-Vendor-Id": "org.zowe.apiml.sdk"
        }
    }

}

configure(subprojects.findAll { it.name in servicesToPublish }) {
    publishing {
        publications {
            mavenJava(MavenPublication) {
                groupId 'org.zowe.apiml'
                version rootProject.version
                artifactId "${project.name}"

                from components.java
            }
        }
    }

    tasks.withType(Jar) {
        manifest {
            attributes "Specification-Title": project.name
            attributes "Specification-Version": rootProject.version
            attributes "Specification-Vendor": "Zowe org"

            attributes "Implementation-Title": project.name
            attributes "Implementation-Version": rootProject.version
            attributes "Implementation-Vendor": "Zowe org"
            attributes "Implementation-Vendor-Id": "org.zowe.apiml"
        }
    }

}

ext.publishTasksList = projectsToPublish.collect {
    ":" + it + ":publish"
} + ":platform:publish"

//noinspection GroovyAssignabilityCheck
task publishSdkArtifacts {
    doLast {
        println 'Published SDK libraries for main version of Spring Boot to Zowe Artifactory'
    }

    group 'Zowe Publishing'
    description 'Publish SDK libraries for main version of Spring Boot to Zowe Artifactory'
    dependsOn publishTasksList
}
