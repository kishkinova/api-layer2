apply plugin: "org.sonarqube"

ext.pullRequest = System.getenv()['CHANGE_ID'] ?: null

sonarqube {
    properties {
        property "sonar.host.url", project.getProperty('sonar.host.url')
        property "sonar.login", project.hasProperty('sonar.login') ? project.getProperty('sonar.login') : null
        property "sonar.sourceEncoding", "UTF-8"
        property "sonar.projectKey", "zowe:api-mediation-layer"
        property "sonar.projectName", "Zowe API Mediation Layer"
        property "sonar.projectVersion", project.version
        property "sonar.language", "java"
        property "sonar.links.scm", "https://github.com/zowe/api-layer"
        property "sonar.links.scm_dev", "https://github.com/zowe/api-layer.git"
        property "sonar.links.ci", project.hasProperty('sonar.links.ci') ? project.getProperty('sonar.links.ci') : null
        if (pullRequest != null) {
            property "sonar.analysis.mode", "preview"
            property "sonar.github.pullRequest", pullRequest
            property "sonar.github.repository", "zowe/api-layer"
            property "sonar.github.oauth",  project.hasProperty('sonar.github.oauth') ? project.getProperty('sonar.github.oauth') : null
        }
    }
}

subprojects {
    sonarqube {
        properties {
            property "sonar.sources", "src/main/java"
            property "sonar.tests", "src/test/java"
        }
    }
}

project(":gateway-service") {
    sonarqube {
        properties {
            property "sonar.exclusions", "src/main/resources/static/**/*"
        }
    }
}

project(":api-catalog-ui") {
    sonarqube {
        skipProject = true
    }
}


// Packaging project; sonar does not apply
project(":zowe-install") {
    sonarqube {
        skipProject = true
    }
}

project(":integration-tests") {
    sonarqube {
        properties {
            property "sonar.sources", "src/test/java"
            property "sonar.test.exclusions", "**/*.java"
            property "sonar.cpd.exclusions", "**/*.java"
            property "sonar.java.binaries", "build/libs"
        }
    }
}
