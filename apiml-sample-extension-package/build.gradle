/*
 * This program and the accompanying materials are made available under the terms of the
 * Eclipse Public License v2.0 which accompanies this distribution, and is available at
 * https://www.eclipse.org/legal/epl-v20.html
 *
 * SPDX-License-Identifier: EPL-2.0
 *
 * Copyright Contributors to the Zowe Project.
 */
ext {
    artifactName = 'apiml-sample-extension-package'
    pullNo = project.hasProperty("pullRequest") && project.getProperty("pullRequest") != null ? "-" + project.getProperty("pullRequest") : ""
}

configurations {
    sampleExtensionJar
    zip
}

dependencies {
    sampleExtensionJar(project(path: ":apiml-sample-extension"))
}

task packageSampleExtension(type: Zip) {
    archiveFileName = artifactName + pullNo + ".zip"
    includeEmptyDirs = true
    def resourceDir = 'src/main/resources/'

    into('/') {
        from "$buildDir/convert/manifest.yaml"
        from "../schemas/apiml-sample-extension-schema.json"
    }

    into('bin/') {
        from configurations.sampleExtensionJar
    }
}

// save build information to manifest
packageSampleExtension.doFirst {
    String manifest = file('src/main/resources/manifest.yaml').text

    def getCommitHash = 'git rev-parse --verify HEAD'.execute()
    def commit_hash = new ByteArrayOutputStream()
    def command_err = new ByteArrayOutputStream()
    getCommitHash.consumeProcessOutput(commit_hash, command_err)
    getCommitHash.waitForOrKill(1000)

    manifest = manifest.replaceAll("\\{\\{build.branch\\}\\}", System.getenv("BRANCH_NAME") ?: "")
        .replaceAll("\\{\\{build.number\\}\\}", System.getenv("BUILD_NUMBER") ?: "")
        .replaceAll("\\{\\{build.commitHash\\}\\}", commit_hash.toString().trim())
        .replaceAll("\\{\\{build.timestamp\\}\\}", (new Date()).getTime().toString())

    def scriptOutputDir = new File("$buildDir/convert/");
    if (!scriptOutputDir.exists()) {
        scriptOutputDir.mkdirs();
    }
    File convertedManifest = new File("$buildDir/convert/manifest.yaml");
    if (convertedManifest.exists()) {
        convertedManifest.delete();
    }
    convertedManifest.createNewFile();
    convertedManifest.write(manifest)
}

jar.dependsOn(packageSampleExtension)
build.dependsOn(packageSampleExtension)

artifacts {
    zip packageSampleExtension
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact("$buildDir/distributions/$artifactName" + pullNo + ".zip")
        }
    }
}

jar {
    enabled false
}
