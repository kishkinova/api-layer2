plugins {
    id 'java-platform'
    id 'maven-publish'
}

ext {
    artifactId = 'api-layer-bom'
}

javaPlatform {
    allowDependencies()
}

/**
 * all transitive dependencies upgrades in the whole project
 * to use in other module use api(platform(project(":platform")))
 */
dependencies {
    api(platform(libs.spring.boot.dependencies))

    api(platform(libs.netty.bom))

    api libs.netty.reactor.core
    api libs.netty.reactor.http

    api libs.jackson.annotations
    api libs.jackson.core
    api libs.jackson.databind
    api libs.jackson.dataformat.yaml

    api libs.joda
    api libs.woodstox
    api libs.guava
    api libs.snakeyaml
    api libs.hystrix.core
    api libs.netflix.commons.util

    api libs.jersey.server

    api libs.amazon.aws.autoscaling
    api libs.amazon.aws.core
    api libs.amazon.aws.ec2
    api libs.amazon.aws.route
    api libs.amazon.aws.sts

    api libs.eureka.client
    api libs.eureka.core
    api libs.spring.cloud.starter.eureka
    api libs.spring.cloud.starter.eureka.server
    api libs.spring.eureka.client.v3
    api libs.spring.cloud.commons

    api libs.swagger3.core
    api libs.swagger3.parser
    api libs.swagger.jaxrs2

    api libs.bcprov
    api libs.bcpkix

    api libs.netflix.servo

    api libs.junitJupiter
    api libs.junit.platform.commons

    api libs.mockito.core
    api libs.mockito.jupiter

    constraints {
        api(libs.spring.security.config) {
            because 'older versions are vulnerable to CVE-2023-20862'
        }
        api(libs.spring.security.core) {
            because 'older versions are vulnerable to CVE-2023-20862'
        }
        api(libs.spring.security.crypto) {
            because 'older versions are vulnerable to CVE-2023-20862'
        }
        api(libs.spring.security.web) {
            because 'older versions are vulnerable to CVE-2023-20862'
        }

        api(libs.spring.aop) {
            because 'older versions are vulnerable to CVE-2023-20863'
        }
        api(libs.spring.beans) {
            because 'older versions are vulnerable to CVE-2023-20863'
        }
        api(libs.spring.context) {
            because 'older versions are vulnerable to CVE-2023-20863'
        }
        api(libs.spring.context.support) {
            because 'older versions are vulnerable to CVE-2023-20863'
        }
        api(libs.spring.core) {
            because 'older versions are vulnerable to CVE-2023-20863'
        }
        api(libs.spring.expression) {
            because 'older versions are vulnerable to CVE-2023-20863'
        }
        api(libs.spring.messaging) {
            because 'older versions are vulnerable to CVE-2023-20863'
        }
        api(libs.spring.web) {
            because 'older versions are vulnerable to CVE-2023-20863'
        }
        api(libs.spring.webmvc) {
            because 'older versions are vulnerable to CVE-2023-20863'
        }
        api(libs.spring.webflux) {
            because 'older versions are vulnerable to CVE-2023-20863'
        }
        api(libs.spring.websocket) {
            because 'older versions are vulnerable to CVE-2023-20863'
        }

        api(libs.commons.io) {
            because 'older versions are vulnerable to CVE-2021-29425'
        }

        api(libs.logback.classic) {
            because "Newer version requires JDK 11"
        }
        api(libs.logback.core) {
            because "Newer version requires JDK 11"
        }

        api(libs.xstream) {
            because 'older versions are vulnerable to CVE-2022-40151, CVE-2022-40152, and CVE-2022-41966'
        }

        api(libs.reactor) {
            because 'version 3.5.x has a confict with spring-security-config:5.7.8'
        }
    }

}

