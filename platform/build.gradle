plugins {
    id 'java-platform'
    id 'maven-publish'
}

ext {
    artifactId = 'api-layer-bom'
}

javaPlatform {
    allowDependencies()
}

/**
 * all transitive dependencies upgrades in the whole project
 * to use in other module use api(platform(project(":platform")))
 */
dependencies {
    api(platform(libs.spring.boot.dependencies))
    api(platform(libs.spring.bom))
    api(platform(libs.netty.bom))

    // enforce version throughout all modules
    constraints {
        // to define preferred version (the minimal version constraints)
        libs.getCommons().owner.config.libraries.values().forEach { x ->
            def version = x.getVersion().getPreferredVersion()
            if (version.isEmpty()) version = x.getVersion().toString()
            api x.getGroup().toString() + ":" + x.getName().toString() + ":" + version
        }

        // to define strict version (including the maximum version)
        api libs.reactor
    }

}

