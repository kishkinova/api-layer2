buildscript {
    repositories mavenRepositories
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("gradle.plugin.com.gorylenko.gradle-git-properties:gradle-git-properties:${gradleGitPropertiesVersion}")
    }
}

normalization {
    runtimeClasspath {
        ignore("**/*git.properties*")
        ignore("**/*build-info.properties*")
    }
}

apply plugin: 'org.springframework.boot'
apply plugin: 'com.gorylenko.gradle-git-properties'
apply plugin: 'com.google.cloud.tools.jib'


jib {
    to {
        image = project.hasProperty("zowe.docker.container") ? project.getProperty("zowe.docker.container") : "ghcr.io/zowe/caching-service"
        auth {
            username = project.hasProperty("zowe.docker.username") ? project.getProperty("zowe.docker.username") : ""
            password = project.hasProperty("zowe.docker.password") ? project.getProperty("zowe.docker.password") : ""
        }
    }

    container {
        args = ['--spring.profiles.include=dev,diag', '--spring.config.additional-location=file:/config/docker/caching-service.yml']
    }

    extraDirectories {
        paths {
            path {
                from = "../config"
                into = "/config"
            }
            path {
                from = "../keystore"
                into = "/keystore"
            }
        }
    }
}

springBoot {
    // This statement tells the Gradle Spring Boot plugin to generate a file
    // build/resources/main/META-INF/build-info.properties that is picked up by Spring Boot to display
    // via /info endpoint
    buildInfo {
        properties {
            // Generate extra build info:
            additionalProperties = [
                by: System.properties['user.name'],
                operatingSystem: "${System.properties['os.name']} (${System.properties['os.version']})",
                number: System.getenv('BUILD_NUMBER') ? System.getenv('BUILD_NUMBER') : "n/a",
                machine: InetAddress.localHost.hostName
            ]
        }
    }
}

gitProperties {
    gitPropertiesDir = new File("${project.rootDir}/${name}/build/resources/main/META-INF")
}

dependencies {
    implementation project(':common-service-core')

    implementation project(':onboarding-enabler-spring')
    implementation('javax.servlet:javax.servlet-api') {
        version {
            strictly '3.1.0'
        }
    }

    implementation libraries.jjwt
    implementation libraries.jjwt_impl
    implementation libraries.jjwt_jackson

    implementation libraries.springFox
    implementation libraries.spring_boot_starter
    implementation libraries.spring_boot_starter_actuator
    implementation libraries.spring_boot_starter_web
    implementation libraries.spring_boot_starter_security
    implementation libraries.spring_boot_starter_actuator
    implementation libraries.spring_retry
    implementation libraries.spring_boot_starter_aop
    implementation libraries.lettuce

    implementation libraries.gson
    compileOnly libraries.lombok
    annotationProcessor libraries.lombok

    testImplementation libraries.spring_boot_starter_test
    testCompile libraries.hamcrest
}


bootJar.archiveName = "${bootJar.baseName}.jar"

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact bootJar
        }
    }
}
