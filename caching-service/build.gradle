buildscript {
    repositories mavenRepositories
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("gradle.plugin.com.gorylenko.gradle-git-properties:gradle-git-properties:${gradleGitPropertiesVersion}")
        classpath ("org.eclipse.jgit:org.eclipse.jgit:5.13.0.202109080827-r") {
            force = true
        }
    }
}

normalization {
    runtimeClasspath {
        ignore("**/*git.properties*")
        ignore("**/*build-info.properties*")
    }
}

apply plugin: 'org.springframework.boot'
apply plugin: 'com.gorylenko.gradle-git-properties'
apply plugin: 'com.google.cloud.tools.jib'
apply from: "../gradle/jib.gradle"

def debugPort = 5126
def componentName = "caching-service"

setJib(componentName, debugPort)

springBoot {
    // This statement tells the Gradle Spring Boot plugin to generate a file
    // build/resources/main/META-INF/build-info.properties that is picked up by Spring Boot to display
    // via /info endpoint
    buildInfo {
        properties {
            // Generate extra build info:
            additionalProperties = [
                by: System.properties['user.name'],
                operatingSystem: "${System.properties['os.name']} (${System.properties['os.version']})",
                number: System.getenv('BUILD_NUMBER') ? System.getenv('BUILD_NUMBER') : "n/a",
                machine: InetAddress.localHost.hostName
            ]
        }
    }
}

gitProperties {
    gitPropertiesDir = new File("${project.rootDir}/${name}/build/resources/main/META-INF")
}

dependencies {
    implementation project(':common-service-core')
    implementation project(':apiml-tomcat-common')
    implementation project(':onboarding-enabler-spring')
    implementation libraries.zowe_attls
    implementation libraries.jjwt
    implementation libraries.jjwt_impl
    implementation libraries.jjwt_jackson

    implementation libraries.springFox
    implementation libraries.spring_boot_starter
    implementation libraries.spring_boot_starter_actuator
    implementation libraries.spring_boot_starter_web
    implementation libraries.spring_boot_starter_security
    implementation libraries.spring_boot_starter_actuator
    implementation libraries.spring_retry
    implementation libraries.spring_boot_starter_aop
    implementation libraries.spring_cloud_starter_hystrix
    implementation libraries.lettuce
    implementation libraries.spring_security_web
    implementation libraries.spring_security_config
    implementation libraries.spring_security_core
    implementation libraries.spring_security_crypto

    implementation libraries.tomcat_annotations_api
    implementation libraries.tomcat_embed_core
    implementation libraries.tomcat_embed_websocket
    implementation libraries.xstream
    implementation libraries.spring_aop
    implementation libraries.spring_beans
    implementation libraries.spring_context
    implementation libraries.spring_context_support
    implementation libraries.spring_core
    implementation libraries.spring_expression
    implementation libraries.spring_messaging
    implementation libraries.spring_web
    implementation libraries.spring_webmvc
    implementation libraries.spring_websocket
    implementation libraries.bcprov;
    implementation libraries.bcpkix;

    implementation libraries.guava
    implementation libraries.gson
    compileOnly libraries.lombok
    annotationProcessor libraries.lombok

    testImplementation libraries.spring_boot_starter_test
    testImplementation libraries.json_smart
    testCompile libraries.hamcrest
    testImplementation libraries.rest_assured
    testImplementation(testFixtures(project(":integration-tests")))
}


bootJar.archiveName = "${bootJar.baseName}.jar"

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact bootJar
        }
    }
}
